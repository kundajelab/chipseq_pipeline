#!/usr/bin/env bds
#vim: syntax=java


help == chipseq pipeline settings

type 		:= "TF" 	help Type of ChIP-Seq pipeline. TF or histone (default: TF).
histone 	:= false	help Histone ChIP-Seq. Equivalent to '-type histone'
final_stage	:= "" 		help Final stage for pipeline (bam, filt_bam, tag, xcor and peak).
true_rep   	:= false	help Call peaks on true replicates only.
no_pseudo_rep	:= false 	help Do not call peaks on self pseudo replicates.
ctl_depth_ratio := 1.2 		help Cut-off ratio of two control tagaligns for pooling (default: 1.2).
idr_thresh 	:= "0.05"	help IDR threshold : -log_10(score) (default: 0.05).
subsample_xcor 	:= "15M"	help # reads to be subsampled for cross corr. analysis (default: 15M).
subsample 	:= "0" 		help # reads to subsample exp. replicate. Subsampled tagalign will be used for steps downstream (default: 0; no subsampling).
subsample_ctl 	:= "0" 		help # reads to subsample control if non-zero (recommended: 40M or lower).
bam2bw 		:= false	help (BETA) Create bigwig from filtered bam (using bamCoverage in deepTools).
tag2bw 		:= false 	help (BETA) Create bigwig from tagalign (using align2rawsignal).
make_wig 	:= false	help (BETA) Create wig (works with '-tag2bw' only).
macs2_for_pooled_rep_only := false 	help Generate MACS2 signal tracks for pooled replicate only.
idr_rank 	:= "signal.value"  	help Scoring column in narrow peak files for IDR. Use signal.value for SPP peaks, and p.value for MACS2 peaks (default: signal.value).

help() // print help and exit if no parameters are given

include "modules/input.bds"

include "modules/align_bwa.bds"
include "modules/postalign_bam.bds"
include "modules/postalign_bed.bds"

include "modules/callpeak_spp.bds"
include "modules/callpeak_macs2.bds"
include "modules/callpeak_naive_overlap.bds"
include "modules/callpeak_etc.bds"
include "modules/idr.bds"

include "modules/signal.bds"



// Important file names are stored in global variables (usually a string map string{} with a key with replicate id and peakcaller name)
// e.g. filt_bam{"rep1"} = filtered bam for replicate 1, peak_pr1{"spp,2"} = peak file for pseudo replicate 1 of replicate 2 generated from spp

string{} filt_bam
string{} tag, tag_pr1, tag_pr2 // replicate data: map with key ("$ctl,$rep" or "$ctl,$rep,$pe" for PE fastqs)
string tag_ppr1, tag_ppr2
string{} peak, peak_pr1, peak_pr2, peak_pooled, peak_ppr1, peak_ppr2 // peaks: map with key ("$rep")
string peak_overlap

string{} tag_ctl, tag_ctl_label // key: replicate id, value: actual control used for each replicate (according to control depth ratio)

string{} idr_tr, idr_pr, idr_tr_png, idr_pr_png
string idr_ppr, idr_opt, idr_consv, idr_ppr_png

string{} sig_trk 	 	// signal tracks from deepTools or bamCoverage: map with key ("$ctl,$rep")
string{} pval_bigwig, fc_bigwig // signal track from macs2

string{} flagstat_qc, dup_qc, flagstat_nodup_qc, pbc_qc, xcor_qc, xcor_plot // QC logs: map with key ("$ctl,$rep" or "$ctl,$rep,$pe" for PE fastqs)

string{} xcor_qc_pr1, xcor_qc_pr2
string xcor_qc_ppr1, xcor_qc_ppr2
string idr_qc


main() 


void main() { // chipseq pipeline starts here

	init_chipseq() // read command line parameters or configruation file

	chk_param() // check if parameters are valid

	chk_input( true_rep, no_pseudo_rep ) // if inputs are fastq, bam, or tagalign, check if they exist
	
	align() // align and postalign

	pool_tags() // make pooled tagaligns and choose appropriate control for each replicate

	call_peaks() // call peaks

	read_input_peak() // if inputs are peaks, read them

	naive_overlap() // get naive overlap peaks

	do_idr() // IDR

	create_sig_trk() // (BETA)

	report()
}

void init_chipseq() {

	read_conf_chipseq()

	print_chipseq()	
}

void read_conf_chipseq() {

	type		= get_conf_val( type,			["type"] )
	histone 	= get_conf_val_bool( histone, 		["histone"] )
	final_stage	= get_conf_val( final_stage, 		["final_stage"] )
	true_rep 	= get_conf_val_bool( true_rep, 		["true_rep"] )
	no_pseudo_rep	= get_conf_val_bool( no_pseudo_rep, 	["no_pseudo_rep"] )
	ctl_depth_ratio = get_conf_val_real( ctl_depth_ratio, 	["ctl_depth_ratio"])		
	idr_thresh 	= get_conf_val( idr_thresh, 		["idr_thresh"] )
	subsample_xcor 	= get_conf_val( subsample_xcor,		["subsample_xcor"] )
	subsample 	= get_conf_val( subsample,		["subsample"] )
	subsample_ctl 	= get_conf_val( subsample_ctl,		["subsample_ctl"] )
	bam2bw 		= get_conf_val_bool( bam2bw, 		["bam2bw"])
	tag2bw 		= get_conf_val_bool( tag2bw, 		["tag2bw"])
	make_wig	= get_conf_val_bool( make_wig, 		["make_wig"])
	macs2_for_pooled_rep_only = get_conf_val_bool( macs2_for_pooled_rep_only, 	["macs2_for_pooled_rep_only"] )
	idr_rank 	= get_conf_val( idr_rank, 		["idr_rank"] )

	if ( histone ) type = "histone"
}

void print_chipseq() {

	print( "\n\n== chipseq pipeline settings\n")
	print( "Type of ChIP-Seq pipeline\t\t: $type\n")
	print( "Final stage for ChIP-Seq\t\t: $final_stage\n" )
	print( "# replicates \t\t\t\t: "+get_num_rep()+"\n" )
	print( "Peak calling for true reps only\t\t: $true_rep\n" )
	print( "No peak calling for self pseudo reps\t: $no_pseudo_rep\n" )
	print( "Control rep. depth ratio\t\t: $ctl_depth_ratio\n" )
	print( "IDR threshold\t\t\t\t: $idr_thresh\n" )
	print( "# reads to subsample for cross-corr. analysis\t\t: "+parse_number( subsample_xcor)+"\n")
	print( "# reads to subsample exp. replicates (0: no subsampling): "+parse_number( subsample )+"\n")
	print( "# reads to subsample controls (0: no subsampling)\t: "+parse_number( subsample_ctl)+"\n")
	print( "Create bigwig from filt_bam\t\t: $bam2bw\n")
	print( "Create bigwig from tagalign\t\t: $tag2bw\n")
	print( "Create wig\t\t\t\t: $make_wig\n" )
	print( "Signal tracks for pooled rep. only\t: $macs2_for_pooled_rep_only\n")	
	print( "Scoring column for IDR\t\t\t: $idr_rank\n")	
}

void chk_param() {

	print( "\n\n== checking chipseq parameters ...\n" );
	if ( has_input_fastq() ) 	chk_align_bwa()
	if ( tag2bw ) 			chk_signal_aln2rawsig()
	if ( is_final_stage_idr() ) 	chk_idr()
	if ( is_final_stage_idr() || is_final_stage_peak() ) chk_callpeak_macs2()

	// errors
	if ( !is_input_peak() && need_spp() && !ctl_exists() && !is_final_stage_before_peak() ) \
		error("Cannot call peaks (SPP) without controls!\n")

	if ( multimapping > 0 ) \
		error("Multimapping is not available for chipseq pipeline!\n")

	if ( is_input_peak() && true_rep && get_num_rep() == 1 ) \
		error("Cannot perform IDR with only one replicate!")

	if ( is_histone_chipseq() && macs2_for_pooled_rep_only ) \
		macs2_for_pooled_rep_only = false

	if ( has_pe_input_tag() && parse_number( subsample_xcor ) > 0 ) \
		print("Warning: PE tagaligns cannot be subsampled for cross-corr. analysis.\n")

	if ( has_pe_input_tag() && parse_number( subsample ) > 0 ) \
		print("Warning: PE tagaligns cannot be subsampled!\n")

	if ( has_pe_input_tag(1) && parse_number( subsample_ctl ) > 0 ) \
		error("Warning: PE contol tagaligns cannot be subsampled!\n")
}

void align() {

	if ( is_input_peak() ) return

	//// distribute # threads for each replicate/control

	int{} filesize

	for ( int ctl=0; ctl <= 1; ctl++) { // iterate through inputs (ctl==0 : exp. replicate, ctl==1 : control)
		if ( ctl==1 && !ctl_exists() ) continue

		for ( int rep=1; rep <= get_num_rep( ctl ); rep++) {		
			if ( !input_file_exists( ctl, rep ) ) continue

			// check file size to distribute_nth nth to each nth_app
			// determine # threads for each app related to alignment

			key := "$ctl,$rep"  // key name for global output variable (map)

			// get file size in bytes
			if ( is_input_fastq( ctl, rep ) ) {

				fastqs := get_fastqs( ctl, rep )
				filesize{key} = (fastqs[0]).size()*3 // multiply 3 (weight) to allocate more cpus for align
				if ( fastqs.size() > 1) filesize{key} += (fastqs[1]).size()
			}
			else if ( is_input_bam( ctl, rep ) ) 		filesize{key} = (get_bam( ctl, rep )).size()
			else if ( is_input_filt_bam( ctl, rep ) ) 	filesize{key} = (get_filt_bam( ctl, rep )).size()
			else if ( is_input_tag( ctl, rep ) ) 		filesize{key} = (get_tag( ctl, rep )).size()*5
		}
	}

	nth_rep := distribute_nth( nth, filesize ) // distribute_nth # threads according to file size

	//// align
	
	for ( int ctl=0; ctl <= 1; ctl++) { // iterate through inputs (ctl==0 : exp. replicate, ctl==1 : control)
		if ( ctl==1 && !ctl_exists() ) continue		

		for ( int rep=1; rep <= get_num_rep( ctl ); rep++) {

			if ( !input_file_exists( ctl, rep ) ) continue

			key := "$ctl,$rep"  // key name for global output variable (map)

			if ( no_par ) _align( ctl, rep, nth_rep{key} ) // parallel jobs for align() for each replicate and each control
			else	  par _align( ctl, rep, nth_rep{key} ) // parallel jobs for align() for each replicate and each control
		}
	}

	wait_clear_tids()

	print( "\n== Done align()\n" )
}

void _align( int ctl, int rep, int nth_rep ) {

	if ( is_se( ctl, rep ) ) 	_align_SE( ctl, rep, nth_rep )
	else 				_align_PE( ctl, rep, nth_rep )

	if ( !no_par ) monitor_par()
}

void _align_SE( int ctl, int rep, int nth_rep ) {

	info 	:= get_info_str( ctl, rep )
	key 	:= "$ctl,$rep"  // key name for global output variable (map)

	aln_o_dir := mkdir( "$out_dir/align/$info" ) // create align output directory
	qc_o_dir  := mkdir( "$out_dir/qc/$info" ) // create qc output dir.

	string bam

	if ( is_input_fastq( ctl, rep ) ) {

		fastqs := get_fastqs( ctl, rep )

		( bam, flagstat_qc{key} ) = _bwa( fastqs[0], aln_o_dir, qc_o_dir, info, nth_rep )
		wait
	}

	string filt_bam_

	if ( is_input_bam( ctl, rep ) || is_input_fastq( ctl, rep ) ) {
	
		if ( is_input_bam( ctl, rep ) ) bam = get_bam( ctl, rep )

		if ( is_final_stage_bam() ) return

		( filt_bam_, dup_qc{key}, flagstat_nodup_qc{key}, pbc_qc{key} ) \
			= _dedup_bam( bam, aln_o_dir, qc_o_dir, info, nth_rep )
		wait
	}

	string tag_

	if ( is_input_filt_bam( ctl, rep ) || is_input_bam( ctl, rep ) || is_input_fastq( ctl, rep ) ) {

		if ( is_input_filt_bam( ctl, rep ) ) filt_bam_ = get_filt_bam( ctl, rep )

		filt_bam{key} = filt_bam_

		if ( is_final_stage_filt_bam() ) return

		tag_ = _bam_to_tag( filt_bam_, aln_o_dir, info )
		wait
	}

	if ( is_input_tag( ctl, rep ) || is_input_filt_bam( ctl, rep ) || is_input_bam( ctl, rep ) || is_input_fastq( ctl, rep ) ) {

		if ( is_input_tag( ctl, rep ) ) tag_ = get_tag( ctl, rep )

		if ( ctl == 0 && parse_number( subsample ) > 0 ) {
			tag_ = _subsample_tag( tag_, parse_number( subsample ), aln_o_dir, info )
			wait
		}
		else if ( ctl > 0 && parse_number( subsample_ctl ) > 0 ) {
			tag_ = _subsample_tag( tag_, parse_number( subsample_ctl ), aln_o_dir, info )
			wait
		}

		tag{key} = tag_

		if ( is_final_stage_tag() ) return

		string xcor_qc_

		if ( ctl == 0 ) { // if replicate

			subsampled_tag := _subsample_tag( tag_, parse_number( subsample_xcor ), aln_o_dir, info )

			string subsampled_tag_pr1, subsampled_tag_pr2

			string qc_pr1_o_dir, qc_pr2_o_dir

			if ( !true_rep ) { // pseudo replicates

				aln_pr1_o_dir := mkdir( "$out_dir/align/pseudo_reps/$info/pr1" )
				aln_pr2_o_dir := mkdir( "$out_dir/align/pseudo_reps/$info/pr2" )

				qc_pr1_o_dir    = mkdir( "$out_dir/qc/pseudo_reps/$info/pr1" ) // create qc output dir.
				qc_pr2_o_dir    = mkdir( "$out_dir/qc/pseudo_reps/$info/pr2" ) // create qc output dir.

				string tag_pr1_, tag_pr2_
				(tag_pr1_, tag_pr2_ ) = _spr( tag_, aln_pr1_o_dir, aln_pr2_o_dir, info ) // make self pseudo replicate
				tag_pr1{key} = tag_pr1_
				tag_pr2{key} = tag_pr2_
				wait
			}

			wait

			// distribute_nth nth_rep for xcor
			nth_xcor := distribute_nth( nth_rep, true_rep ? [1] : [2,1,1] ) // for [true-rep,pr1,pr2]

			// xcor for true rep
			( xcor_qc_, xcor_plot{key} ) = _xcor( subsampled_tag, qc_o_dir, info, nth_xcor[0], info )
			xcor_qc{key} = xcor_qc_
		}

		if ( is_final_stage_xcor() ) return
	}	
}

void _align_PE( int ctl, int rep, int nth_rep ) {

	info 	:= get_info_str( ctl, rep )
	key 	:= "$ctl,$rep"  // key name for global output variable (map)

	aln_o_dir := mkdir( "$out_dir/align/$info" ) // create align output directory
	qc_o_dir  := mkdir( "$out_dir/qc/$info" ) // create qc output dir.

	string bam

	if ( is_input_fastq( ctl, rep ) ) {

		fastqs := get_fastqs( ctl, rep )

		( bam, flagstat_qc{key} ) = _bwa_PE( fastqs[0], fastqs[1], aln_o_dir, qc_o_dir, info, nth_rep )
		wait
	}

	string filt_bam_

	if ( is_input_bam( ctl, rep ) || is_input_fastq( ctl, rep ) ) {

		if ( is_input_bam( ctl, rep ) ) bam = get_bam( ctl, rep )

		if ( is_final_stage_bam() ) return

		( filt_bam_, dup_qc{key}, flagstat_nodup_qc{key}, pbc_qc{key} ) \
			= _dedup_bam_PE( bam, aln_o_dir, qc_o_dir, info, nth_rep )
		wait
	}

	string tag_, bedpe
	
	if ( is_input_filt_bam( ctl, rep ) || is_input_bam( ctl, rep ) || is_input_fastq( ctl, rep ) ) {

		if ( is_input_filt_bam( ctl, rep ) ) filt_bam_ = get_filt_bam( ctl, rep )

		filt_bam{key} = filt_bam_

		if ( is_final_stage_filt_bam() ) return

		bedpe = _bam_to_bedpe( filt_bam_, aln_o_dir, info )
		wait

		if ( ctl == 0 && parse_number( subsample ) > 0 ) {
			bedpe = _subsample_bedpe( bedpe, parse_number( subsample ), aln_o_dir, info )
			wait
		}
		else if ( ctl > 0 && parse_number( subsample_ctl ) > 0 ) {
			bedpe = _subsample_bedpe( bedpe, parse_number( subsample_ctl ), aln_o_dir, info )
			wait
		}

		tag_ = _bedpe_to_tag( bedpe, aln_o_dir, info )
		wait
	}

	if ( is_input_tag( ctl, rep ) || is_input_filt_bam( ctl, rep ) || is_input_bam( ctl, rep ) || is_input_fastq( ctl, rep ) ) {

		if ( is_input_tag( ctl, rep ) ) tag_ = get_tag( ctl, rep )

		tag{key} = tag_

		if ( is_final_stage_tag() ) return

		if ( ctl == 0 ) { // if replicate

			string subsampled_tag

			if ( bedpe == "" ) \
				subsampled_tag = tag_
			else \
				subsampled_tag = _subsample_bedpe_to_tag_xcor( bedpe, parse_number( subsample_xcor ), aln_o_dir, info )

			string tag_pr1_, tag_pr2_
			string qc_pr1_o_dir, qc_pr2_o_dir

			if ( !true_rep ) { // if replicate

				aln_pr1_o_dir := mkdir( "$out_dir/align/pseudo_reps/$info/pr1" )
				aln_pr2_o_dir := mkdir( "$out_dir/align/pseudo_reps/$info/pr2" )

				qc_pr1_o_dir    = mkdir( "$out_dir/qc/pseudo_reps/$info/pr1" ) // create qc output dir.
				qc_pr2_o_dir    = mkdir( "$out_dir/qc/pseudo_reps/$info/pr2" ) // create qc output dir.

				if ( bedpe == "" ) { // if starting from tag, we don't have bedpe file
					( tag_pr1_, tag_pr2_ ) = _spr_tag_PE( tag_, aln_pr1_o_dir, aln_pr2_o_dir, info )
					tag_pr1{key} = tag_pr1_
					tag_pr2{key} = tag_pr2_
				}
				else {
					( tag_pr1_, tag_pr2_ ) = _spr_PE( bedpe, aln_pr1_o_dir, aln_pr2_o_dir, info )
					tag_pr1{key} = tag_pr1_
					tag_pr2{key} = tag_pr2_
				}
			}

			wait

			// distribute_nth nth_rep for xcor
			nth_xcor := distribute_nth( nth_rep, true_rep ? [1] : [2,1,1] ) // for [true-rep,pr1,pr2]

			string xcor_qc_
			( xcor_qc_, xcor_plot{key} ) = _xcor( subsampled_tag, qc_o_dir, info, nth_xcor[0], info )
			xcor_qc{key} = xcor_qc_
		}

		if ( is_final_stage_xcor() ) return
	}
}

void pool_tags() {

	if ( !(is_final_stage_peak() || is_final_stage_idr()) ) return

	if ( is_input_peak() ) return

	////////////// pool replicates and controls, then create ppr (pooled pseudoreplicates) before peak calling

	string[] tags, tags_pr1, tags_pr2, tags_ctl
	int[] ids_ctl

	for ( int rep=1; rep<=get_num_rep(); rep++ ) {

		tags.add( tag{"0,$rep"} )

	 	if ( !true_rep ) {
			tags_pr1.add( tag_pr1{"0,$rep"} )
			tags_pr2.add( tag_pr2{"0,$rep"} )
		}

		if ( input_file_exists( 1, rep ) ) { // if control exists
			tags_ctl.add( tag{"1,$rep"} )
			ids_ctl.add( rep )
		}
	}

	nth_xcor := distribute_nth( nth, [1,1] )

	if ( tags.size() > 1 ) {

	 	aln_pooled_o_dir := mkdir( "$out_dir/align/pooled_rep" )

		tag_pooled := _pool_tag( tags, aln_pooled_o_dir, "reps" )
		tag{"0,pooled"} = tag_pooled

			// graphviz, filetable for pooled tagalign
			string[] graph_in
			for ( int rep=1; rep<=get_num_rep(); rep++ ) graph_in.add("tagalign_(rep$rep)")

			_add_to_graphviz( graph_in, tags, ["tagalign_(pooled)"], [tag_pooled] )
			_add_to_filetable(["L1_align/pooled_rep/tagalign"], [tag_pooled] )
			//

	 	if ( !true_rep ) {
		 	aln_ppr1_o_dir   := mkdir( "$out_dir/align/pooled_pseudo_reps/ppr1" )
		 	aln_ppr2_o_dir   := mkdir( "$out_dir/align/pooled_pseudo_reps/ppr2" )

			qc_ppr1_o_dir    := mkdir( "$out_dir/qc/pooled_pseudo_reps/ppr1" ) // create qc output dir.
			qc_ppr2_o_dir    := mkdir( "$out_dir/qc/pooled_pseudo_reps/ppr2" ) // create qc output dir.

			tag_ppr1_ := _pool_tag( tags_pr1, aln_ppr1_o_dir, "reps-pr1" )
			tag_ppr2_ := _pool_tag( tags_pr2, aln_ppr2_o_dir, "reps-pr2" )
			tag_ppr1 = tag_ppr1_ // for thread safety
			tag_ppr2 = tag_ppr2_
			wait

				// graphviz, filetable for ppr
				string[] graph_in_pr1, graph_in_pr2
				for ( int rep=1; rep<=get_num_rep(); rep++ ) {
					graph_in_pr1.add("tagalign_(rep$rep-pr1)")
					graph_in_pr2.add("tagalign_(rep$rep-pr2)")
				}

				_add_to_graphviz( graph_in_pr1, tags_pr1, ["tagalign_(ppr1)"], [tag_ppr1] )
				_add_to_graphviz( graph_in_pr2, tags_pr2, ["tagalign_(ppr2)"], [tag_ppr2] )
				_add_to_filetable(["L1_align/pooled_pseudo_reps/ppr1/tagalign"], [tag_ppr1] )
				_add_to_filetable(["L1_align/pooled_pseudo_reps/ppr2/tagalign"], [tag_ppr2] )
				//

			string tmp
			(xcor_qc_ppr1, tmp) = _xcor( tag_ppr1_, qc_ppr1_o_dir, "ppr1", nth_xcor[0], "pooled_pseudo_reps/ppr1" )
			(xcor_qc_ppr2, tmp) = _xcor( tag_ppr2_, qc_ppr2_o_dir, "ppr2", nth_xcor[1], "pooled_pseudo_reps/ppr2" )
		}
	}

	if ( tags_ctl.size() > 1 ) {

		aln_pooled_ctl_o_dir := mkdir( "$out_dir/align/pooled_ctl" )

		tag_ctl_pooled := _pool_tag( tags_ctl, aln_pooled_ctl_o_dir, "ctls" )
		tag{"1,pooled"} = tag_ctl_pooled

			// graphviz, filetable for pooled control tagalign
			string[] graph_in
			for ( int ctl : ids_ctl ) graph_in.add("tagalign_(ctl$ctl)")

			_add_to_graphviz( graph_in, tags_ctl, ["tagalign_(ctl, pooled)"], [tag_ctl_pooled] )
			_add_to_filetable(["L1_align/pooled_ctl/tagalign"], [tag_ctl_pooled] )
			//		
	}

	wait_clear_tids()

	/////////// choose appropriate control for each replicate

	if ( tags_ctl.size() == 0 ) { // if no control at all

		tag_ctl{"pooled"} 	= ""
		tag_ctl_label{"pooled"} = ""

		for ( int rep=1; rep<=get_num_rep(); rep++ ) {

			tag_ctl{rep} 		= ""
			tag_ctl_label{rep} 	= ""
		}
	}
	else if ( tags_ctl.size() == 1 ) { // if only one control, use it for all replicates

		tag_ctl{"pooled"} 	= tags_ctl[0]
		tag_ctl_label{"pooled"} = "ctl" + ids_ctl[0]

		for ( int rep=1; rep<=get_num_rep(); rep++ ) {

			tag_ctl{rep} 		= tags_ctl[0]
			tag_ctl_label{rep} 	= "ctl" + ids_ctl[0]
		}
	}
	else { // if multiple controls, check # of lines in replicate/control tagaligns and apply ctl_depth_ratio

		tag_ctl{"pooled"} 		= tag{"1,pooled"}
		tag_ctl_label{"pooled"} 	= "ctl, pooled"		

		real{} nlines 		// # of lines in tagaligns, key: 0,rep for replicate, 1,rep for control
		real[] nlines_ctl 	// # of lines in control tagaligns

		for ( int rep=1; rep<=get_num_rep(); rep++ ) {

			nlines{"0,$rep"} = get_no_lines( tag{"0,$rep"} )

			//print("DEBUG: # lines rep $rep tag.: " + nlines{"0,$rep"} + ", " + tag{"0,$rep"} + "\n")

			if ( input_file_exists( 1, rep ) ) { // if control exists

				nlines{"1,$rep"} = get_no_lines( tag{"1,$rep"} )
				nlines_ctl.add( nlines{"1,$rep"} )

				//print("DEBUG: # lines ctl $rep tag.: " + nlines{"1,$rep"} + ", " + tag{"1,$rep"} + "\n")
			}
		}

		use_pooled_ctl := false

		for ( int i=0; i<nlines_ctl.size(); i++ ) { // check every # lines in every pair of control tagaligns
			for ( int j=i+1; j<nlines_ctl.size(); j++ ) {

				if ( 	nlines_ctl[i] / nlines_ctl[j] > ctl_depth_ratio || \
					nlines_ctl[j] / nlines_ctl[i] > ctl_depth_ratio ) {

					use_pooled_ctl = true

					print("\nNumber of reads in controls differ by a factor of $ctl_depth_ratio. Using pooled controls.\n" )

					break
				}
			}
		}

		for ( int rep=1; rep<=get_num_rep(); rep++ ) {

			if ( !use_pooled_ctl && input_file_exists( 1, rep ) ) {
			
				if ( nlines{"1,$rep"} < nlines{"0,$rep"} ) {

					print("\nFewer reads in control $rep than experiment replicate $rep. Using pooled controls for replicate $rep.\n")

					tag_ctl{rep} 		= tag{"1,pooled"}
					tag_ctl_label{rep} 	= "ctl, pooled"
				}
				else {
					tag_ctl{rep} 		= tag{"1,$rep"}
					tag_ctl_label{rep} 	= "ctl$rep"
				}
			}
			else {

				tag_ctl{rep} 		= tag{"1,pooled"}
				tag_ctl_label{rep} 	= "ctl, pooled"
			}
		}
	}

	wait_clear_tids()

	print( "\n== Done pool_tags()\n" )
}

void call_peaks() {

	if ( !(is_final_stage_peak() || is_final_stage_idr()) ) return

	if ( is_input_peak() ) return

	// distribute_nth # threads according to peak callers, priority for spp than macs2 (4 vs 1)
	// spp: multi-threaded, macs2: single threaded
	int{} nth_peak  

	no_spp 	 := !need_spp()
	no_macs2 := !need_macs2()

	if ( !no_spp && !no_macs2 ) 	( nth_peak{"spp"}, nth_peak{"macs2"} ) = distribute_nth( nth, [4,1] )
	else if ( !no_spp ) 		nth_peak{"spp"} = nth
	else if ( !no_macs2 ) 		nth_peak{"macs2"} = nth

	if ( !no_spp ) {
		if ( no_par ) _call_peaks( "spp", nth_peak{"spp"} )
		else 	  par _call_peaks( "spp", nth_peak{"spp"} )
	}

	if ( !no_macs2 ) {
		if ( no_par ) _call_peaks( "macs2", nth_peak{"macs2"} )
		else 	  par _call_peaks( "macs2", nth_peak{"macs2"} )
	}

	wait_clear_tids()

	print( "\n== Done call_peaks()\n" )	
}

void _call_peaks( string pc, int nth_peak ) { // pc: pc

	if ( pc == "spp" && !ctl_exists() ) return

	//// calculate fragment length (required for SPP and MACS2)

	string fraglen_ppr1, fraglen_ppr2, fraglen_mean
	string{} fraglen, fraglen_pr1, fraglen_pr2 // key = replicate id
	real fraglen_sum = 0.0

	for (int rep=1; rep<=get_num_rep(); rep++) {

		fraglen{rep} = get_fraglen( xcor_qc{"0,$rep"} ) // get fragment length of replicate 1

		if ( !true_rep ) {

			fraglen_pr1{rep} = fraglen{rep}
			fraglen_pr2{rep} = fraglen{rep}
		}

		fraglen_sum += fraglen{rep}.parseReal()
	}

	real num_rep_real = get_num_rep()
	fraglen_mean = round( fraglen_sum / num_rep_real ) // compute fraglen mean for pooled

	if ( !true_rep && get_num_rep() > 1 ) {
		fraglen_ppr1 = fraglen_mean
		fraglen_ppr2 = fraglen_mean
	}

	//// distribute # threads for each peak calling

	int nth_pooled, nth_ppr, nth_true, nth_pr

	if ( !true_rep ) {
		( nth_pooled, nth_ppr ) = distribute_nth( nth_peak, [ 2, 1, 1 ] ) // pooled, ppr1, ppr2
		( nth_true, nth_pr ) 	= distribute_nth( nth_peak, [ 2, 1, 1 ] ) // for each replicate (true, pr1, pr2)
	}
	else {
		( nth_pooled, nth_true ) = distribute_nth( nth_peak, [ get_num_rep(), 1 ] ) // pooled, true
	}

	//// call peaks

	// create directories
	peak_o_dir 	:= mkdir( "$out_dir/peak/$pc") // peak directory structure
	sig_o_dir 	:= mkdir( "$out_dir/signal/$pc") // signal directory structure

	// call peaks on pooled replicates and pooled pseudo replicates
	if ( get_num_rep() > 1 ) {

		pooled_o_dir	:= mkdir( "$peak_o_dir/pooled_rep" )
		pooled_sig_o_dir:= mkdir( "$sig_o_dir/pooled_rep" )

		pooled_hrchy 	:= "pooled_rep" 

		peak_pooled{"$pc"}= _call_peaks( pc, tag{"0,pooled"}, tag_ctl{"pooled"}, fraglen_mean, true, pooled_o_dir, pooled_sig_o_dir, \
							"pooled", tag_ctl_label{"pooled"}, nth_pooled, pooled_hrchy )
		sleep( 4 )

		if ( !true_rep ) {

			ppr1_o_dir 	:= mkdir( "$peak_o_dir/pooled_pseudo_reps/ppr1" )
			ppr1_sig_o_dir 	:= mkdir( "$sig_o_dir/pooled_pseudo_reps/ppr1" )
			ppr2_o_dir 	:= mkdir( "$peak_o_dir/pooled_pseudo_reps/ppr2" )
			ppr2_sig_o_dir 	:= mkdir( "$sig_o_dir/pooled_pseudo_reps/ppr2" )

			ppr1_hrchy 	:= "pooled_pseudo_reps/ppr1"
			ppr2_hrchy 	:= "pooled_pseudo_reps/ppr2"

			if ( !macs2_for_pooled_rep_only || pc != "macs2" ) \
				peak_ppr1{"$pc"} = _call_peaks( pc, tag_ppr1, tag_ctl{"pooled"}, fraglen_ppr1, false, ppr1_o_dir, ppr1_sig_o_dir, \
								"ppr1", tag_ctl_label{"pooled"}, nth_ppr, ppr1_hrchy )

			if ( !macs2_for_pooled_rep_only || pc != "macs2" ) \
				peak_ppr2{"$pc"} = _call_peaks( pc, tag_ppr2, tag_ctl{"pooled"}, fraglen_ppr2, false, ppr2_o_dir, ppr2_sig_o_dir, \
								"ppr2",	tag_ctl_label{"pooled"}, nth_ppr, ppr2_hrchy )
		}
	}

	// call peaks for each replicate and pseudo replicates for it
	for (int rep=1; rep<=get_num_rep(); rep++) {

		o_dir	:= mkdir( "$peak_o_dir/rep$rep" )
		sig_dir := mkdir( "$sig_o_dir/rep$rep" )

		hrchy 	:= "rep$rep"

		if ( !macs2_for_pooled_rep_only || pc != "macs2" ) \
			peak{"$pc,$rep"} = _call_peaks( pc, tag{"0,$rep"}, tag_ctl{rep}, fraglen{rep}, true, o_dir, sig_dir, \
							"rep$rep", tag_ctl_label{rep}, nth_true, hrchy )
		sleep( 4 )

		if ( !true_rep ) {

			pr1_o_dir 	:= mkdir( "$peak_o_dir/pseudo_reps/rep$rep/pr1" )
			pr1_sig_o_dir 	:= mkdir( "$sig_o_dir/pseudo_reps/rep$rep/pr1" )
			pr2_o_dir 	:= mkdir( "$peak_o_dir/pseudo_reps/rep$rep/pr2" )
			pr2_sig_o_dir 	:= mkdir( "$sig_o_dir/pseudo_reps/rep$rep/pr2" )

			pr1_hrchy 	:= "pseudo_reps/rep$rep/pr1"
			pr2_hrchy 	:= "pseudo_reps/rep$rep/pr2"

			if ( !no_pseudo_rep ) {

				if ( !macs2_for_pooled_rep_only || pc != "macs2" ) \
					peak_pr1{"$pc,$rep"} = _call_peaks( pc, tag_pr1{"0,$rep"}, tag_ctl{rep}, fraglen_pr1{rep}, false, pr1_o_dir, pr1_sig_o_dir, \
									"rep$rep-pr1", tag_ctl_label{rep}, nth_pr, pr1_hrchy )

				if ( !macs2_for_pooled_rep_only || pc != "macs2" ) \
					peak_pr2{"$pc,$rep"} = _call_peaks( pc, tag_pr2{"0,$rep"}, tag_ctl{rep}, fraglen_pr2{rep}, false, pr2_o_dir, pr2_sig_o_dir, \
									"rep$rep-pr2", tag_ctl_label{rep}, nth_pr, pr2_hrchy )
			}
		}
	}

	if ( !no_par ) monitor_par()	
}

string _call_peaks( string peakcaller, string tag1, string tag1_ctl, string fraglen, bool make_sig, \
			string peak_o_dir, string sig_o_dir, string label, string label_ctl, \
			int nth_peak, string hrchy ) {
	
	if ( peakcaller == "spp" ) {

		string regionpeak, score, pdf 

		( regionpeak, score, pdf ) \
			= _spp( tag1, tag1_ctl, fraglen, peak_o_dir, label, label_ctl, nth_peak, hrchy )

		return regionpeak // use regionpeak instead of narrowpeak
	}
	else if ( peakcaller == "macs2" ) {

		string narrowpeak, gpeak, fc_bw, pval_bw

		( narrowpeak, gpeak, fc_bw, pval_bw ) \
			= _macs2( tag1, tag1_ctl, fraglen, make_sig, peak_o_dir, sig_o_dir, label, label_ctl, hrchy )

		// macs2 generates signal tracks
		if ( make_sig ) {
 			pval_bigwig{label} 		= pval_bw
			fc_bigwig{label} 		= fc_bw
		}

		return narrowpeak // use narrowpeak
	}
}

void read_input_peak() {

	if ( !is_input_peak() ) return // read peaks here

	for ( int rep=0; rep<=get_num_rep_peak(); rep++) { // rep==0 : pooled
		if ( get_num_rep_peak() == 1 && rep==0 ) continue // if only one replicate, skip reading pooled rep

		for (int pse=0; pse<=2; pse++) { // pse==0 : true rep
			if ( true_rep && pse > 0 ) continue
			if ( no_pseudo_rep && rep != 0 && pse > 0 ) continue

			peak_ := get_peak(rep,pse)

			pc := get_peak_caller()

			if ( rep == 0 ) {
				if ( pse == 0 )		peak_pooled{"$pc"} 	= peak_
				else if ( pse == 1 )	peak_ppr1{"$pc"} 	= peak_
				else if ( pse == 2 )	peak_ppr2{"$pc"} 	= peak_
			}
			else {
				if ( pse == 0 )		peak{"$pc,$rep"} 	= peak_
				else if ( pse == 1 )	peak_pr1{"$pc,$rep"} 	= peak_
				else if ( pse == 2 )	peak_pr2{"$pc,$rep"} 	= peak_
			}
		}
	}
}

void naive_overlap() {

	if ( !(is_final_stage_peak() || is_final_stage_idr()) ) return
	if ( !peak_exists() ) return // if no peaks from peak caller exit

	// peak caller
	pc := get_peak_caller()

	// naive overlap peak

	overlap_o_dir := mkdir( "$out_dir/peak/$pc/overlap" )

	if ( get_num_rep() == 1 ) {

		if ( !true_rep ) {

			if ( !no_pseudo_rep ) \
				peak_overlap = _naive_overlap_peak( "regionPeak", peak{"$pc,1"}, peak_pr1{"$pc,1"}, peak_pr2{"$pc,1"}, overlap_o_dir, "peak", \
									"peak_$pc", "$pc/overlap")
		}
	}
	else {
		string[] peaks // make array of peaks from true replicates
		for ( int rep=1; rep<=get_num_rep(); rep++ ) peaks.add( peak{"$pc,$rep"} )

		peak_ppr1_ := true_rep ? "" : peak_ppr1{"$pc"}
		peak_ppr2_ := true_rep ? "" : peak_ppr2{"$pc"}

		peak_overlap = _naive_overlap_peak( "regionPeak", peak_pooled{"$pc"}, peaks, \
								  peak_ppr1_, peak_ppr2_, overlap_o_dir, "peak",\
								"peak_$pc", "$pc/overlap")
	}

	wait_clear_tids()

	print( "\n== Done naive_overlap()\n" )
}

void do_idr() {

	if ( !is_final_stage_idr() ) return
	if ( is_histone_chipseq() ) return
	if ( !peak_exists() ) return // if no peaks from peak caller exit

	// peak caller
	pc := get_peak_caller()

	idr_o_dir 	:= mkdir( "$out_dir/peak/idr" )

	// IDR on true replicates (on every pair of peaks from replicates)

	for ( int i=1; i<=get_num_rep(); i++ ) {

		for ( int j=i+1; j<=get_num_rep(); j++ ) {

			idr_true_o_dir 	:= mkdir( "$idr_o_dir/true_reps/rep$i-rep$j" )

			(idr_tr{"$i,$j"}, idr_tr_png{"$i,$j"} ) = _idr2( peak{"$pc,$i"}, peak{"$pc,$j"}, peak_pooled{"$pc"}, idr_thresh, idr_rank, idr_true_o_dir, "rep$i-rep$j", \
									"peak_$pc", ["rep$i","rep$j","pooled"], "true_reps/rep$i-rep$j" )
		}

		if ( !true_rep ) {

			idr_pr_o_dir := mkdir( "$idr_o_dir/pseudo_reps/rep$i" )

                        if ( !no_pseudo_rep ) \
				(idr_pr{i}, idr_pr_png{i}) = _idr2( peak_pr1{"$pc,$i"}, peak_pr2{"$pc,$i"}, peak{"$pc,$i"}, idr_thresh, idr_rank, idr_pr_o_dir, "rep$i-pr", \
								"peak_$pc", ["rep$i-pr1","rep$i-pr2","rep$i"], "pseudo_reps/rep$i" )
		}
	}

	if ( !true_rep && get_num_rep() > 1 ) {

		idr_ppr_o_dir := mkdir( "$idr_o_dir/pooled_pseudo_reps" )

		(idr_ppr, idr_ppr_png) = _idr2( peak_ppr1{"$pc"}, peak_ppr2{"$pc"}, peak_pooled{"$pc"}, idr_thresh, idr_rank, idr_ppr_o_dir, "ppr", \
							"peak_$pc", ["ppr1","ppr2","pooled"], "pooled_pseudo_reps" )
	}

	wait_clear_tids()

	qc_o_dir    := mkdir( "$out_dir/qc" ) // create qc output dir.

	// get final idr qc score, use idr final idr narrow peak files from true, pseudo and pooled pseudo reps
	(idr_qc, idr_opt, idr_consv) = _idr_final_qc( idr_tr, idr_pr, idr_ppr, idr_o_dir, qc_o_dir, "" )

	wait_clear_tids()

	print( "\n== Done do_idr()\n" )	
}

void create_sig_trk() {

	if ( !bam2bw && !tag2bw ) return

	for ( int rep=1; rep <= get_num_rep(); rep++) {

		if ( no_par ) _create_sig_trk( rep )
		else	  par _create_sig_trk( rep )
	}

	wait_clear_tids()

	print( "\n== Done create_sig_trk()\n" )	
}

void _create_sig_trk( int rep ) {

	key := "0,$rep"  // key name for global output variable (map)
	info:= "rep$rep"
	if ( bam2bw && filt_bam.hasKey(key) ) { // signal track generation (deeptools)

		sig_o_dir := mkdir( "$out_dir/signal/bam2bw/$info" )

		sig_trk{key} = _bam_to_bw( filt_bam{key}, sig_o_dir, info, 1 )
	}

	if ( tag2bw && tag.hasKey(key) && xcor_qc.hasKey(key) ) {

		sig_o_dir := mkdir( "$out_dir/signal/tag2bw/$info" )

		fraglen := get_fraglen( xcor_qc{key} )
		sig_trk{key} = _tag_to_bw( tag{key}, fraglen, sig_o_dir, info )
		
		if ( make_wig ) _tag_to_wig( tag{key}, fraglen, sig_o_dir, info )
	}
}

void report() {

	wait_clear_tids()

	html := _html_filetable() 	// treeview for directory and file structure 
	html += _html_chipseq_tracks() 	// epigenome browser tracks
	html += _html_graphviz()	// graphviz workflow diagram
	html += _html_chipseq_QC()	// show QC tables and images

	report( html )

	print( "\n== Done report()\n" )		
}

string _html_chipseq_QC() {

	string[] flagstat_qcs, dup_qcs, flagstat_nodup_qcs, pbc_qcs, xcor_qcs, xcor_plots
	string[] flagstat_headers, dup_headers, flagstat_nodup_headers, pbc_headers, xcor_headers

	for ( int ctl=0; ctl <= 1; ctl++) { // iterate through inputs (ctl==0 : exp. replicate, ctl==1 : control)	
		if ( ctl==1 && !ctl_exists() ) continue		
	
		for ( int rep=1; rep <= get_num_rep( ctl ); rep++) {

			if ( !input_file_exists( ctl, rep ) ) continue

			info := get_info_str( ctl, rep )
			key := "$ctl,$rep"

			if ( ctl == 0 ) {
				//html_rep_by_id += _html_xcor( info, 	[ info ], [ xcor_qc{key} ], [ xcor_plot{key} ], [ info ] )

				if ( xcor_qc.hasKey( key ) )	{
					xcor_qcs 		+= xcor_qc{key}
					xcor_plots 		+= xcor_plot{key}

					xcor_headers 		+= info
				}
			}

			if ( flagstat_qc.hasKey( key ) ) 	flagstat_qcs 		+= flagstat_qc{key}
			if ( dup_qc.hasKey( key ) ) 		dup_qcs 		+= dup_qc{key}
			if ( flagstat_nodup_qc.hasKey( key ) )	flagstat_nodup_qcs 	+= flagstat_nodup_qc{key}
			if ( pbc_qc.hasKey( key ) ) 		pbc_qcs			+= pbc_qc{key}

			if ( flagstat_qc.hasKey( key ) ) 	flagstat_headers 	+= info
			if ( dup_qc.hasKey( key ) ) 		dup_headers 		+= info
			if ( flagstat_nodup_qc.hasKey( key ) )	flagstat_nodup_headers 	+= info
			if ( pbc_qc.hasKey( key ) ) 		pbc_headers		+= info

		}
	}	

	html := "<div id='chipseq_qc'>"
	
	html += _parse_flagstat_to_html( "all", 	flagstat_headers, flagstat_qcs, flagstat_headers )
	html += _parse_dup_to_html( "all", 		dup_headers, dup_qcs, dup_headers )
	html += _parse_flagstat_to_html( "all, filtered",flagstat_nodup_headers, flagstat_nodup_qcs, flagstat_nodup_headers )
	html += _parse_pbc_to_html( "all", 		pbc_headers, pbc_qcs, pbc_headers )
	html += _parse_xcor_to_html( "all", 		xcor_headers, xcor_qcs, xcor_plots, xcor_headers )

	// if idr qc's exists, add them to html
	if ( idr_qc != "" ) 		html += _parse_idr_to_html( "idr", idr_qc )

	for ( int i=1; i<=get_num_rep(); i++ ) {
		
		for ( int j=i+1; j<=get_num_rep(); j++ ) {
			if ( idr_tr_png.hasKey("$i,$j") ) html += _html_img( idr_tr_png{"$i,$j"}, 800, "true reps (rep$i-rep$j)" ) + "&nbsp"
		}

		if ( !true_rep ) {
			if ( idr_pr_png.hasKey(i) ) html += _html_img( idr_pr_png{i}, 800, "rep$i pseudo-reps" ) + "&nbsp"
		}
	}		

	if ( idr_ppr_png != "" ) 	html += _html_img( idr_ppr_png, 800, "pooled pseudo-reps" ) + "&nbsp"

	html += "</div><br>"
	return html
}

string _html_chipseq_tracks() {

	string[] trk_files, trk_types, trk_names

	if ( pval_bigwig.hasKey( "pooled" ) ) { trk_types += "bigwig"; trk_names += "$title pval (pooled)"; trk_files += pval_bigwig{"pooled"} }
	if ( fc_bigwig.hasKey( "pooled" ) )   { trk_types += "bigwig"; trk_names += "$title fc (pooled)";   trk_files += fc_bigwig{"pooled"}   }

	if ( peak_overlap != "" ) { trk_types += "hammock"; trk_names += "$title peak overlap"; trk_files += _peak_to_hammock( peak_overlap ) }

	if ( idr_opt != "" ) {	trk_types += "hammock"; trk_names += "$title peak idr (opt. set)"; trk_files += _peak_to_hammock( _get_idr_peak_trk( idr_opt ) ) }
	if ( idr_consv != "" ) {trk_types += "hammock"; trk_names += "$title peak idr (cons. set)"; trk_files += _peak_to_hammock( _get_idr_peak_trk( idr_consv ) ) }

	for (int rep=1; rep<=get_num_rep(); rep++) {

		if ( pval_bigwig.hasKey( "rep$rep" ) ) { trk_types += "bigwig"; trk_names += "$title pval (rep$rep)"; trk_files += pval_bigwig{"rep$rep"} }
		if ( fc_bigwig.hasKey( "rep$rep" ) )   { trk_types += "bigwig"; trk_names += "$title fc (rep$rep)";   trk_files += fc_bigwig{"rep$rep"}   }
		if ( idr_pr.hasKey(rep) ) {trk_types += "hammock"; trk_names += "$title peak idr (rep$rep-pr)"; trk_files += _peak_to_hammock( _get_idr_peak_trk( idr_pr{rep} ) ) }
	}

	html := _html_epg_browser_viz( trk_files, trk_types, trk_names )

	return html
}

void help() {

	if ( is_cmd_line_arg_empty() ) {
		printHelp()
		exit
	}
}

bool is_final_stage_bam() {

	return final_stage.toLower() == "bam"
}

bool is_final_stage_filt_bam() {

	return final_stage.toLower() == "filt_bam"
}

bool is_final_stage_tag() {

	return final_stage.toLower() == "tag" || final_stage.toLower() == "tagalign"
}

bool is_final_stage_xcor() {

	return final_stage.toLower() == "xcor"
}

bool is_final_stage_peak() {

	return final_stage.toLower() == "peak"
}

bool is_final_stage_idr() {

	return (final_stage.toLower() == "idr") || (final_stage.toLower() == "")
}

bool is_final_stage_before_peak() {

	return is_final_stage_bam() || is_final_stage_filt_bam() || is_final_stage_tag() || is_final_stage_xcor()
}

bool is_TF_chipseq() {

	return type.toLower() == "tf"
}

bool is_histone_chipseq() {

	return type.toLower() == "histone"
}

string get_peak_caller() {

	if ( is_TF_chipseq() ) 		 return "spp"
	else if ( is_histone_chipseq() ) return "macs2"	
	else 				 return ""
}

bool peak_exists() {

	pc := get_peak_caller()

	return peak.hasKey("$pc,1")
}

bool need_spp() {

	return !is_histone_chipseq()
}

bool need_macs2() {

	return true
}